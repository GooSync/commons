apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

ext.versions = [
    minSdk            : 17,
    compileSdk        : 28,
    targetSdk         : 28,

    versionCode       : 1,
    versionName       : '0.2.2',

    buildTools        : '28.0.3',
    support           : '28.0.0',
    constraint        : '1.1.3',

    dagger            : '2.11',

    firebase_core     : '16.0.5',
    firebase_messaging: '17.3.4',
    firebase_ads      : '17.1.1',

    mobmetricalib     : '3.0.0',
    mobmetricapushlib : '1.1.0',

    facebook          : '4.35.0',
    stetho            : '1.5.0',

    crashlytics       : '2.9.3',

    rxjava            : '2.1.13',
    rxandroid         : '2.0.2',
    gson              : '2.8.5'
]

android {
  compileSdkVersion versions.compileSdk
  buildToolsVersion versions.buildTools

  defaultConfig {
    minSdkVersion versions.minSdk
    targetSdkVersion versions.targetSdk
    versionCode versions.versionCode
    versionName versions.versionName

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  testImplementation 'junit:junit:4.12'
//  androidTestImplementation 'com.android.support.test:runner:1.0.2'
//  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

  api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

  api "com.android.support:design:$versions.support"
  api "com.android.support.constraint:constraint-layout:$versions.constraint"
  api "com.android.support:support-v4:$versions.support"
  api "com.android.support:appcompat-v7:$versions.support"
  api "com.android.support:recyclerview-v7:$versions.support"


  // Firebase
  implementation "com.google.firebase:firebase-core:$versions.firebase_core"
  implementation "com.google.firebase:firebase-messaging:$versions.firebase_messaging"
  implementation "com.google.firebase:firebase-ads:$versions.firebase_ads"

  // Yandex Metrica
  api "com.yandex.android:mobmetricalib:$versions.mobmetricalib"
  api "com.yandex.android:mobmetricapushlib:$versions.mobmetricapushlib"

  // Facebook
  api "com.facebook.android:facebook-android-sdk:$versions.facebook"
  api "com.facebook.stetho:stetho:$versions.stetho"

  // Dagger
  api "com.google.dagger:dagger:$versions.dagger"
  kapt "com.google.dagger:dagger-compiler:$versions.dagger"

  // Fabric
  api("com.crashlytics.sdk.android:crashlytics:$versions.crashlytics") {
    transitive = true
  }

  // RxJava
  api "io.reactivex.rxjava2:rxjava:$versions.rxjava"
  api "io.reactivex.rxjava2:rxandroid:$versions.rxandroid"

  // Other
  api "com.google.code.gson:gson:$versions.gson"
}